body file control
{
  namespace => "lynis";
}

bundle agent globals
{
  vars:
    !windows::
      "version"      string => "2.6.4";
      "archive_hash" string => "487dc69cb8c484f05b8d8dc3e425ede0af91cb3524e7e69ed6d83a675ac16e18";

      "hash_type"    string => "sha256";
      "tar_url"      string => "https://downloads.cisofy.com/lynis/lynis-$(version).tar.gz";

      "max_report_age" string => "1";

      "install_prefix" string => "/opt/lynis";
      "report_data" string => "/var/log/lynis-report.dat";
}

bundle agent main
{
  meta:

    default:services_autorun.!windows::

      "tags" slist => { "autorun" };

  methods:

    !windows::

      "globals";
      "report_freshness";
      "install_tar_gz";
      "audit";
      "report_format";
      "inventory";

}
bundle agent report_freshness
{
  vars:
    !windows::
      "max_age" string => "$(globals.max_age)";
      "data" string => "$(globals.report_data)";
      "prefix" string => "$(globals.install_prefix)";

  files:
    !windows::
      "$(report_data)"
        delete => default:tidy,
        file_select => default:days_old( $(max_age) );

      "$(prefix)/lynis-report.json"
        delete => default:tidy,
        file_select => default:days_old( $(max_age) );
}

bundle agent install_tar_gz
{
  vars:
    !windows::
      "url[lynis]"     string => "https://github.com/CISOfy/lynis/archive/$(globals.version).tar.gz";
      "url[converter]" string => "https://raw.githubusercontent.com/d4t4king/lynis-report-converter/master/lynis-report-converter.pl";
      "lynis_archive"  string => "$(globals.install_prefix)/lynis-$(globals.version).tar.gz";
      "converter"      string => "$(globals.install_prefix)/lynis-report-converter.pl";
      "lynis"          string => "$(globals.install_prefix)/lynis-$(globals.version)/lynis";
      "prefix"         string => "$(globals.install_prefix)";
      "hash_type"      string => "$(globals.hash_type)";
      "archive_hash"   string => "$(globals.archive_hash)";
      "version"        string => "$(globals.version)";

  files:
    !windows::
      "$(prefix)/."
        comment => "The directory that we are installing to needs to exist or
                    downloads will fail.",
        create => "true";

  packages:

    centos|redhat::

      "perl-Module-Load-Conditional"
        policy => "present";

      "perl-JSON"
        policy => "present";

      "perl-Data-Dumper"
        policy => "present";

  commands:

    !windows::

      # Download the lynis-report-converter to $(converter) if it does not exist

      "/usr/bin/curl"
        if => not( fileexists( $(converter) ) ),
        arglist => {
                     "--silent",
                     "$(url[converter])",
                     "--output",
                     "$(converter)",
                     "-k"
                   };

      # Download $(lynis_archive) if it does not match the
      # $(globals.hash_type) digest of $(globals.archive_hash)

      "/usr/bin/curl"
        handle => "fetch_lynis_archive",
        arglist => {
                     "$(url[lynis])",
                     "-L",
                     "--max-redirs",
                     "3",
                     "--output",
                     "$(lynis_archive)",
                     "-k"
                   },
        if => not(strcmp(
                          file_hash( $(lynis_archive), $(hash_type) ),
                          $(archive_hash)
                        ));

      # Unpack $(lynis_archive) into $(globals.install_prefix) if
      # $(lynis_archive) matches the $(globals.hash_type) digest of
      # $(globals_archive_hash) AND
      # $(globals.install_prefix)/lynis-$(globals.version) does
      # not exist.

      "/bin/tar"
        arglist => {
                     "-zxf",
                     "$(lynis_archive)",
                     "-C",
                     "$(prefix)"
                   },
        if => and(strcmp(
                          file_hash( $(lynis_archive), $(hash_type) ),
                          $(archive_hash)),
                  not( fileexists( "$(prefix)/lynis-$(version)") ) );
}

bundle agent audit
# @brief Run lynis from the unpacked tar file if $(globals.report_data) is
# not available.
{
  vars:

    !windows::

      "report_data" string => "$(globals.report_data)";
      "lynis" string => "$(install_tar_gz.lynis)";
      "lynis_dir" string => dirname( $(lynis) );

  commands:

    !windows::

     "$(lynis)"
        contain => default:in_dir_shell( "$(lynis_dir)" ),
        arglist => { "audit", "system", "--quiet", "--quick" },
        if => not( fileexists( $(report_data) ) );
}

bundle agent report_format
# @brief Ensure there is an easily consumable version of the lynis report data
{
  vars:

    !windows::

      "report_data" string => "$(globals.report_data)";
      "converter"   string => "$(install_tar_gz.converter)";
      "prefix"      string => "$(globals.install_prefix)";

  commands:

      "/usr/bin/perl"
        arglist => {
                     $(converter),
                     "--input",
                     "/var/log/lynis-report.dat",
                     "--json",
                     "--output",
                     "$(prefix)/lynis-report.json"
                   },
        if => not(fileexists( "$(prefix)/lynis-report.json" ));
}

bundle agent inventory
# @brief Parse the JSON version of the lynis report data and inventory interesting data
{
  vars:

    !windows::

      "prefix"      string => "$(globals.install_prefix)";

      "d"
        data => readjson("$(prefix)/lynis-report.json", inf ),
        if => fileexists("$(prefix)/lynis-report.json");

      "s" slist => getindices( "d[suggestion[]]" );
      "detail_ids" slist => getindices( "d[details[]]" );

      # Inventory the high level suggestions for CFEngine Enterprise Mission Portal
      "suggestion_id[$(d[suggestion[]][$(s)][id])]"
        string => "$(d[suggestion[]][$(s)][id])",
        meta => { "inventory", "attribute_name=CISOfy Lynis Suggestion"};

      # Inventory the suggestion details for CFEngine Enterprise Mission Portal
      "detail[$(detail_ids)]"
        string => "$(d[details[]][$(detail_ids)][id]) $(d[details[]][$(detail_ids)][description][desc]). $(d[details[]][$(detail_ids)][description][field]) prefers $(d[details[]][$(detail_ids)][description][prefval]) over  $(d[details[]][$(detail_ids)][description][value])",
        meta => { "inventory", "attribute_name=CISOfy Lynis Suggestion $(d[details[]][$(detail_ids)][id])"};

      # Inventory Lynis Security Hardening index
      "hardening_index"
        string => "$(d[hardening_index])",
        meta => { "inventory", "attribute_name=CISOfy Lynis Hardening Index"};

 reports:

    inform_mode.!windows::
     # Report suggestion details

     "bundle $(this.bundle): $(d[details[]][$(detail_ids)][id]) $(d[details[]][$(detail_ids)][description][desc]). $(d[details[]][$(detail_ids)][description][field]) prefers $(d[details[]][$(detail_ids)][description][prefval]) over  $(d[details[]][$(detail_ids)][description][value])";
}
